#===============================================================================
# Global states (not halted by Pause/SuperPause, no helper limitations)
#===============================================================================
[StateDef -4]

if pauseTime = 0 {
	# For backward compatibility, x and z-axis get hit acceleration must be handled here
	# TODO: These cases should maybe use the state number constants
	# TODO: These patches could be done by appending sctrl's to the common1.cns states
	if time > 0 && (stateNo = 5030 || stateNo = 5035 || stateNo = 5040 || stateNo = 5050 || stateNo = 5071 || stateNo = 5200) {
		velAdd{x: getHitVar(xAccel); z: getHitVar(zAccel)}
	}
	# When keepstate attack, pause at hitshaketime are handled here
	# TODO: will change the pause process from hitpause to a new pause SCTRL
	if gethitvar(keepstate) && gethitvar(hitshaketime)>0 && hitpausetime=0 && !HitOverridden {
		ModifyPlayer{hitpausetime:gethitvar(hitshaketime)}
		ignorehitpause{
		GetHitVarSet{hitshaketime:0}
		}
	}
}

[StateDef +1]

# For backward compatibility, z-axis get hit velocity must be handled here
if time = 0 && pauseTime = 0 {
	if stateNo = 151 || stateNo = 153 || stateNo = 155 || stateNo = 5001 || stateNo = 5011 || stateNo = 5030 || stateNo = 5071 || stateNo = 5081 {
		hitVelSet{z: 1}
	}
}

# Ground dust effects
if gameVar(pausetime) = 0 && gameVar(superpausetime) = 0 {
	if (moveType = H && (stateType = S || stateType = C)) || stateNo = 52 {
		if pos y = 0 && (Abs(pos x + cameraPos x - map(_iksys_dustposx)) >= 1 || Abs(pos z - map(_iksys_dustposz)) >= 1) {
			makeDust{spacing: 3}
		}
	}
}
mapSet{map: "_iksys_dustposx"; value: pos x + cameraPos x}
mapSet{map: "_iksys_dustposz"; value: pos z}

# Round start maps reset
if !isHelper && roundTime = 0 {
	map(_iksys_lifeRecoveryFlag) := 0;
}

if !isHelper && win && alive && roundState = 4 && map(_iksys_lifeRecoveryFlag) = 0
	&& (((teamMode = Turns || enemy,teamMode = Turns) && !matchOver)
		|| (gameMode = "survival" || gameMode = "survivalcoop" || gameMode = "netplaysurvivalcoop")) {
	if ratioLevel > 0 {
		let bonus = lifeMax * gameOption(options.ratio.recovery.bonus) / 100;
		let base = lifeMax * gameOption(options.ratio.recovery.base) / 100;
	} else {
		let bonus = lifeMax * gameOption(options.turns.recovery.bonus) / 100;
		let base = lifeMax * gameOption(options.turns.recovery.base) / 100;
	}
	let timeRatio = float(timeRemaining) / (timeRemaining + timeElapsed);
	if timeRemaining < 0 {
		let timeRatio = 1;
	}
	let lifeRecovery = round($base + $timeRatio * $bonus, 0);
	lifeSet{value: min(life + $lifeRecovery, lifeMax)}
	map(_iksys_lifeRecoveryFlag) := 1;
}
